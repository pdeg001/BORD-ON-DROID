Build1=Default,nl.pdeg.bordondroid
File1=Baloo2-Regular.ttf
File10=wacht_op_data.png
File2=clvBorden.bal
File3=digital-7.ttf
File4=main.bal
File5=materialdesignicons-webfont.ttf
File6=mouse.png
File7=ServerBoard.bal
File8=sven_oud.jpg
File9=sven1.jpg
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=core
Library2=jmqtt
Library3=randomaccessfile
Library4=json
Library5=customlistview
Library6=phone
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="21" android:targetSdkVersion="28"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~
Module1=mqttGetBordData
Module2=mqttGetBords
Module3=ServerBoard
Module4=Starter
NumberOfFiles=10
NumberOfLibraries=6
NumberOfModules=4
Version=9.801
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: Board On Droid
	#VersionCode: 1
	#VersionName: 0.11
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: landscape
	#CanInstallToExternalStorage: False
	#BridgeLogger: true
#End Region

#Region  Activity Attributes 
	#FullScreen: true
	#IncludeTitle: false
#End Region

Sub Process_Globals
	Type Message (Body As String, From As String)
	Type bordStatus(ip As String, name As String, timeStamp As Long, alive As Boolean)
	Type bordFound(name As String, ip As String)
	
	Private mqttGetBord As mqttGetBords
	Private mqttGetData As mqttGetBordData
	Private tmrBordLastAlive As Timer
End Sub

Sub Globals
	Private clvServer As CustomListView
	Private lblBordName As Label
	Private lblViewBord As Label
	Private pnlBord As Panel
	Private lblLastCheck As Label
	Private pnlNobords As Panel
'	Private B4XLoadingIndicator1 As B4XLoadingIndicator
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Activity.LoadLayout("main")
	clvServer.Clear
	mqttGetData.Initialize
	mqttGetBord.Initialize
	mqttGetBord.Connect

	tmrBordLastAlive.Initialize("tmrBordAlive", 10000)
	tmrBordLastAlive.Enabled = True
End Sub

Sub tmrBordAlive_Tick
	ServerAlive
End Sub

Sub Activity_Resume
	If Starter.mqttGetBordsActive Then
		mqttGetBord.Connect
	End If
End Sub

Sub Activity_Pause (UserClosed As Boolean)
 	DisconnectMqtt
End Sub

Private Sub Activity_KeyPress(KeyCode As Int) As Boolean
	If KeyCode = KeyCodes.KEYCODE_BACK Then
		tmrBordLastAlive.Enabled = False
		Activity.Finish
		
		Return False
	Else
		Return False
	End If
End Sub

Sub DisconnectMqtt
	If mqttGetBord.Connected Then
		mqttGetBord.Disconnect
	End If
	
End Sub

Sub genUnitList(name As String, alive As Boolean) As Panel
	Dim p As Panel
	p.Initialize(Me)
	p.SetLayout(0dip, 0dip, clvServer.AsView.Width, 85dip) '190
	p.LoadLayout("clvBorden")
	p.Tag = name
	
	lblBordName.Text = name.Trim
	lblLastCheck.Text = $"Laatste controle $Time{DateTime.Now}"$
	If Not(alive) Then
		lblViewBord.Enabled = False
	End If
	Return p
End Sub

Sub CheckIpExits(bord As String)
	
	Dim ipFound As Boolean = False
	Dim name As String = ""
	
	If bord.Length = 0 Then Return
	
	name = bord

	If Starter.serverList.Size = 0 Then
		AddUnkownIp("", name)
		Return
	End If
	
	For Each lst As bordStatus In Starter.serverList
		If lst.name = name Then
			ipFound = True
			lst.timeStamp = DateTime.Now
			lst.alive = True
		End If
	Next
	
	ServerAlive
	
	If Not(ipFound) Then
		AddUnkownIp("", name)
		Return
	End If
End Sub

Sub ServerAlive
'	Log($"BORD ALIVE TEST at $Time{DateTime.Now}"$)
	Dim msNow As Long = DateTime.Now
	For Each lst As bordStatus In Starter.serverList
		If(msNow - lst.timeStamp) > Starter.serverDied And lst.alive = True Then
			lst.alive = False
			CheckIpInClv(lst)
			Return
		Else
			CheckIpInClv(lst)
		End If
	Next
End Sub

Sub AddUnkownIp(ip As String, name As String)
	Dim bordStatus As bordStatus
	bordStatus.Initialize
	bordStatus.ip = ip
	bordStatus.name = name
	bordStatus.timeStamp = DateTime.Now
	bordStatus.alive = True
	Starter.serverList.Add(bordStatus)
	CheckIpInClv(bordStatus)
	
	If clvServer.GetSize > 0 Then
		pnlNobords.SetVisibleAnimated(1000, False)
	Else 
		pnlNobords.SetVisibleAnimated(1000, True)	
	End If
	Sleep(1000)
	
End Sub

Sub CheckIpInClv(bord As bordStatus)
	Dim p As Panel
	Dim lbl As Label
	
	For i = 0 To clvServer.GetSize-1
		p = clvServer.GetPanel(i)
		If p.Tag = bord.name Then
			For Each v As View In p.GetAllViewsRecursive
				If v.Tag = "lblLastCheck" Then
					lbl = v
					lbl.Text = $"Laatste controle $Time{DateTime.Now}"$
				End If
			Next
			
			For Each v As View In p.GetAllViewsRecursive
				If v.Tag = "viewbord" Then
					lbl = v
					If bord.alive = False Then
						lbl.Enabled = False
						lbl.TextColor = Colors.Red
					Else
						lbl.Enabled = True
						lbl.TextColor = 0xFF027F00
					End If
					Return
				End If
			Next
		End If
	Next
	
'	Log("------ ADD BORD TO LIST")
	pnlNobords.SetVisibleAnimated(1000, False)
	clvServer.Add(genUnitList(bord.name, bord.alive), 130dip, "")
End Sub

Sub lblViewBord_Click
	Dim v As View = Sender
	Dim p As Panel = v.Parent
	Dim lbl As Label
	
	
	For Each c As View In p.GetAllViewsRecursive
		If c.Tag = "name" Then
			lbl = c
			Starter.selectedBordName = PrepTopicName(lbl.Text)
			Starter.DiscoveredServer = lbl.Text
			Exit
		End If
	Next
	
	DisconnectMqtt
	Sleep(100)

	StartActivity(ServerBoard)
End Sub

Private Sub PrepTopicName(bord As String) As String
	Log($"SUBBORD : ${bord.Replace(" ", "")}"$)
	Return bord.Replace(" ", "")
End Sub







